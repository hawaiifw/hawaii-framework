buildscript {
    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "http://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.5.0'
        classpath("org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16")
        classpath "ru.vyarus:gradle-quality-plugin:2.2.0"
    }
}

ext {
    bouncycastleVersion               = "1.58"
    javaVersion                       = "1.8"
    jasyptVersion                     = "1.9.2"
    springPlatformVersion             = "Brussels-SR5"
    logbackVersion                    = "1.1.5"
    nimbusDsVersion                   = "4.27"
    javadocLinks = [
        "http://commons.apache.org/proper/commons-lang/javadocs/api-3.5/",
        "http://docs.oracle.com/javase/8/docs/api/",
        "http://docs.oracle.com/javaee/7/api/",
        "http://docs.spring.io/spring/docs/4.3.11.RELEASE/javadoc-api/",
        "http://docs.spring.io/spring-boot/docs/1.5.7.RELEASE/api/",
        "http://fasterxml.github.io/jackson-core/javadoc/2.8/",
        "http://fasterxml.github.io/jackson-databind/javadoc/2.8/",
        "http://fasterxml.github.io/jackson-datatype-json-org/javadoc/2.8/",
        "http://fasterxml.github.io/jackson-datatype-jsr310/javadoc/2.8/",
        "http://hamcrest.org/JavaHamcrest/javadoc/1.3/"
    ] as String[]
}

ext.coreprojects = subprojects.findAll { project -> !project.name.startsWith("hawaii-samples") }
ext.sampleprojects = subprojects.findAll { project -> project.name.startsWith("hawaii-samples") }

configure(allprojects) { project ->

    group = "org.hawaiiframework"

    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "idea"

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/snapshot" }
    }

    configurations {
        all*.exclude group: "commons-logging", module: "commons-logging"
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springPlatformVersion}"
        }
    }

    dependencies {
        compile("org.slf4j:jcl-over-slf4j")
        compile("org.slf4j:slf4j-api")
        compile("commons-io:commons-io")
        testCompile("junit:junit")
        testCompile("org.mockito:mockito-core")
        testCompile("org.springframework.boot:spring-boot-starter-logging")
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    test {
        outputs.upToDateWhen { false }
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configure(subprojects - sampleprojects) { subproject ->

    apply plugin: "maven"
    apply plugin: "signing"
    apply plugin: "ru.vyarus.quality"

    jar {
        manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
        manifest.attributes["Implementation-Title"] = subproject.name
        manifest.attributes["Implementation-Version"] = subproject.version

        from("${rootProject.projectDir}/src/dist") {
            include "license.txt"
            include "notice.txt"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }

    javadoc {
        description = "Generates project-level Javadoc API documentation."

        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.links(javadocLinks)
        options.addStringOption("Xdoclint:none", "-quiet")

        // Suppress warnings due to cross-module @see and @link references;
        // Note that global 'api' task does display all warnings.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    quality {
        configDir = "${rootProject.projectDir}/src/quality/config/"
        checkstyle = true
        findbugs = true
        pmd = true
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: System.getenv("SONATYPE_OSSRH_USERNAME"), password: System.getenv("SONATYPE_OSSRH_PASSWORD"))
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: System.getenv("SONATYPE_OSSRH_USERNAME"), password: System.getenv("SONATYPE_OSSRH_PASSWORD"))
                }

                pom.project {
                    name "Hawaii Framework"
                    packaging "jar"
                    description "Hawaii Framework"
                    url "https://github.com/hawaiifw/hawaii-framework"

                    scm {
                        connection "scm:git@github.com/hawaiifw/hawaii-framework.git"
                        developerConnection "scm:git@github.com:hawaiifw/hawaii-framework.git"
                        url "https://github.com/hawaiifw/hawaii-framework"
                    }

                    licenses {
                        license {
                            name "The Apache License, Version 2.0"
                            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id "marceloverdijk"
                            name "Marcel Overdijk"
                            email "marcel@overdijk.me"
                        }
                    }
                }
            }
        }
    }
}

project("hawaii-async") {

    description = "Hawaii Async"

    dependencies {
        compile project(":hawaii-core")
        compile project(":hawaii-logging")
        compile "org.springframework:spring-core"
        compile "org.springframework.boot:spring-boot"
        compile "org.springframework:spring-context"
        compile "org.apache.httpcomponents:httpclient"
        compile "javax.validation:validation-api"
    }
}

project("hawaii-autoconfigure") {

    description = "Hawaii Autoconfigure"

    dependencies {
        compile project(":hawaii-core")
        compileOnly project(":hawaii-async")
        compileOnly project(":hawaii-cache-memcached")
        compileOnly project(":hawaii-cache-redis")
        compileOnly project(":hawaii-logging")
        compile "org.hibernate:hibernate-validator"
        compile "org.springframework.boot:spring-boot"
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile "org.springframework.boot:spring-boot-test"
        compile "org.springframework.data:spring-data-redis"
        compile "org.yaml:snakeyaml"
        compileOnly "com.fasterxml.jackson.core:jackson-databind"
        compileOnly "com.fasterxml.jackson.datatype:jackson-datatype-json-org:${dependencyManagement.importedProperties['jackson.version']}"
        compileOnly "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        compileOnly "javax.servlet:javax.servlet-api"
        testCompile "com.fasterxml.jackson.core:jackson-databind"
        testCompile "com.fasterxml.jackson.datatype:jackson-datatype-json-org:${dependencyManagement.importedProperties['jackson.version']}"
        testCompile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        testCompile("org.springframework:spring-test")
        testCompile("org.hamcrest:hamcrest-core")
        testCompile("org.hamcrest:hamcrest-library")
        testCompile "redis.clients:jedis"
        testCompile project(":hawaii-logging")
    }
}

project("hawaii-cache") {

    description = "Hawaii Cache"

    dependencies {
        compile project(":hawaii-core")
    }
}

project("hawaii-cache-memcached") {

    description = "Hawaii Cache Memcached"

    dependencies {
        compile project(":hawaii-cache")
        compileOnly project(":hawaii-logging")
        compile 'net.spy:spymemcached:2.10.4'
    }
}

project("hawaii-cache-redis") {

    description = "Hawaii Cache Redis"

    dependencies {
        compile project(":hawaii-core")
        compile project(":hawaii-cache")
        compileOnly project(":hawaii-logging")
        compile "javax.validation:validation-api"
        compile "redis.clients:jedis"
        compile "org.springframework.data:spring-data-redis"
    }
}

project("hawaii-core") {

    description = "Hawaii Core"

    dependencies {
        compile "org.apache.commons:commons-lang3"
        compile "org.hamcrest:hamcrest-core"
        compile "org.hamcrest:hamcrest-library"
        compile "org.jasypt:jasypt:${jasyptVersion}"
        compile "org.bouncycastle:bcprov-jdk15on:${bouncycastleVersion}"
        compileOnly "com.fasterxml.jackson.core:jackson-databind"
        compileOnly "javax.servlet:javax.servlet-api"
        compileOnly "org.aspectj:aspectjweaver"
        compileOnly "org.springframework:spring-aop"
        compileOnly "org.springframework:spring-core"
        compileOnly "org.springframework:spring-context"
        compileOnly "org.springframework:spring-web"
        compileOnly "org.springframework:spring-webmvc"
        testCompile "org.springframework:spring-core"
    }
}

project("hawaii-errors") {

    description = "Hawaii Errors"

    dependencies {
        compile project(":hawaii-core")
        compile "org.springframework:spring-web"
    }
}

project("hawaii-logging") {

    description = "Hawaii Logging"

    dependencies {
        compile project(":hawaii-core")
        compile "org.springframework:spring-web"
        compile "ch.qos.logback:logback-core:${logbackVersion}"
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "javax.servlet:javax.servlet-api:3.1.0"
        compile 'net.sourceforge.findbugs:annotations:1.3.2'
    }
}

project("hawaii-security") {

    description = "Hawaii Security"

    dependencies {
        compile project(":hawaii-core")
        compile project(":hawaii-async")
        compile project(":hawaii-cache")
        compile project(":hawaii-errors")
        compile "org.springframework:spring-web"
        compile "org.springframework.boot:spring-boot-starter-security"
        compile "org.springframework.security:spring-security-jwt"
        compile("com.nimbusds:nimbus-jose-jwt:${nimbusDsVersion}")
        compile "javax.servlet:javax.servlet-api:3.1.0"
        compile "com.fasterxml.jackson.core:jackson-databind"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-json-org:${dependencyManagement.importedProperties['jackson.version']}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        compile "org.json:json"
    }
}

project("hawaii-starter") {

    description = "Hawaii Starter"

    dependencies {
        compile project(":hawaii-autoconfigure")
        compile project(":hawaii-core")
        compile "org.springframework.boot:spring-boot-starter"
        compile "org.springframework.boot:spring-boot-starter-aop"
    }
}

project("hawaii-starter-rest") {

    description = "Hawaii Starter Rest"

    dependencies {
        compile project(":hawaii-starter")
        compile "com.fasterxml.jackson.core:jackson-databind"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-json-org:${dependencyManagement.importedProperties['jackson.version']}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        compile "org.json:json"
    }
}

project("hawaii-starter-test") {

    description = "Hawaii Starter Test"

    dependencies {
        compile project(":hawaii-test")
    }
}

project("hawaii-starter-async") {

    description = "Hawaii Starter Async"

    dependencies {
        compile project(":hawaii-starter")
        compile project(":hawaii-async")
    }
}

project("hawaii-starter-logging") {

    description = "Hawaii Starter Logging"

    dependencies {
        compile project(":hawaii-starter")
        compile project(":hawaii-logging")
    }
}

project("hawaii-stereotype") {

    description = "Hawaii Stereotype"

    dependencies {
        compile "org.springframework:spring-context"
    }
}

project("hawaii-test") {

    description = "Hawaii Test"

    dependencies {
        compile project(":hawaii-core")
        compile "com.jayway.jsonpath:json-path"
        compile "com.jayway.jsonpath:json-path-assert"
        compile "junit:junit"
        compile "org.hamcrest:hamcrest-core"
        compile "org.hamcrest:hamcrest-library"
        compile "org.mockito:mockito-core"
        compile "org.skyscreamer:jsonassert"
        compile "org.springframework:spring-beans"
        compile "org.springframework:spring-core"
        compile "org.springframework:spring-test"
        compile "org.springframework:spring-web"
    }
}

project("hawaii-utils") {

    description = "Hawaii Logging"

    dependencies {
        compile project(":hawaii-core")
        compile "org.springframework:spring-web"
        compile "org.springframework.security:spring-security-core"
        compile "ch.qos.logback:logback-core:${logbackVersion}"
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "javax.servlet:javax.servlet-api:3.1.0"
        compile "javax.mail:mail:1.4.7"
        compile "com.nulab-inc:zxcvbn:1.2.3"
    }
}

configure(rootProject) {
    description = "Hawaii Framework"

    apply plugin: "org.asciidoctor.convert"

    // Add plugin so we can use Ruby gems when rendering documents.
    apply plugin: "com.github.jruby-gradle.base"

    // Don't publish the default jar for the root project.
    configurations.archives.artifacts.clear()

    dependencies {
        // Gems dependency configuration is added by jruby-gradle-plugin. Here we define the gems we need in our build.
        gems "rubygems:asciidoctor-diagram:1.2.0"
    }

    asciidoctorj {
        // We can change the AsciidoctorJ dependency version.
        version = "1.5.4"
    }

    asciidoctor {

        // jrubyPrepareGems task is added by the JRuby plugin and will download
        // Ruby gems we have defined in the gems dependency configuration.
        dependsOn jrubyPrepare

        // Asciidoctor task needs the asciidoctor-diagram gem, which we installed with
        // the jrubyPrepareGems task and gems dependency configuration.
        requires = ['asciidoctor-diagram']

        // Set path to find gems to directory used by jrubyPrepare task.
        gemPath = jrubyPrepare.outputDir

        backends = ["html5", "pdf"]

        sourceDir project.file("src/asciidoc")

        attributes  "revnumber": project.version,
                    "spring-version": "${dependencyManagement.importedProperties['spring.version']}",
                    "spring-boot-version": "${dependencyManagement.importedProperties['spring-boot.version']}"

        // Split resources in 'real' images and generated resources (by asciidoctor-diagram)
        // so we can ignore the generated once. Bundle in a single resources directory because
        // the resources method takes a single CopySpec, which only has one target directory.
        resources {
            from(project.file("src/asciidoc/resources")) {
                include "**/*.svg"
                include "**/*.png"
            }
            into "./resources"
        }
    }

    task api(type: Javadoc) {
        group = "Documentation"
        description = "Generates aggregated Javadoc API documentation."
        title = "${rootProject.description} ${version} API"

        dependsOn {
            coreprojects.collect {
                it.tasks.getByName("jar")
            }
        }
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = rootProject.description
        options.overview = "src/api/overview.html"
        options.stylesheetFile = file("src/api/stylesheet.css")
        options.splitIndex = true
        options.links(javadocLinks)
        options.addStringOption("Xdoclint:none", "-quiet")

        source coreprojects.collect { project ->
            project.sourceSets.main.allJava
        }

        classpath += files(coreprojects.collect { it.sourceSets.main.compileClasspath })
        destinationDir = new File(buildDir, "api")
        maxMemory = "1024m"
    }

    task docs(dependsOn: ["api", "asciidoctor"]) {
        group = "Documentation"
        description = "Generates aggregated Javadoc API and reference documentation."
    }

    task docsZip(type: Zip, dependsOn: "docs") {
        group = "Distribution"
        baseName = "hawaii-framework"
        classifier = "docs"
        description = "Builds -${classifier} archive containing Javadoc API and reference documentation."

        from (api) {
            into "api"
        }

        from (asciidoctor) {
            include "html5/**/*.*"
            include "pdf/index.pdf"
            into "reference"
            rename { filename ->
                filename.replace "index", "reference-documentation"
            }
        }
    }

    task distZip(type: Zip, dependsOn: "docsZip") {
        group = "Distribution"
        baseName = "hawaii-framework"
        classifier = "dist"
        description = "Builds -${classifier} archive containing all jars and documentation."

        from("src/dist") {
            include "license.txt"
            include "notice.txt"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }

        from(zipTree(docsZip.archivePath)) {
            into "docs"
        }

        coreprojects.each { subproject ->
            into ("libs") {
                from subproject.jar
                if (subproject.tasks.findByPath("sourcesJar")) {
                    from subproject.sourcesJar
                }
                if (subproject.tasks.findByPath("javadocJar")) {
                    from subproject.javadocJar
                }
            }
        }
    }

    artifacts {
        archives docsZip
        archives distZip
    }

    task wrapper(type: Wrapper) {
        description = "Generates gradlew[.bat] scripts"
        gradleVersion = "4.1"
    }
}
